&НаКлиенте
Перем ЗакрытиеФормыПодтверждено;

#Область Процедуры_и_функции

#Область Основные
&НаСервере
Процедура CompareDataOnServer(СтруктураПараметровОтКлиента, ТекстОшибок)

	//Если источник А - файл, хранящийся на клиентском компьютере
	Если Объект.BaseTypeA = 3 И Объект.ConnectingToExternalBaseADeviceStorageFile = 1 Тогда
		//Сохранение пути к исходному файлу
		PathToFileAatKlient = Объект.ConnectionToExternalBaseAPathToFile;
		//Создание временного файла на сервере
		ФайлА = ПолучитьИзВременногоХранилища(СтруктураПараметровОтКлиента.АдресВременногоХранилищаФайлаА); 
		PathToFileAatServer = ПолучитьИмяВременногоФайла(Объект.ConnectionToExternalBaseAFileFormat);
		ФайлА.Записать(PathToFileAatServer);
		Объект.ConnectionToExternalBaseAPathToFile = PathToFileAatServer;
	КонецЕсли;
	
	//Если источник Б - файл, хранящийся на клиентском компьютере
	Если Объект.BaseTypeB = 3 И Объект.ConnectingToExternalBaseBDeviceStorageFile = 1 Тогда
		//Сохранение пути к исходному файлу
		PathToFileBatKlient = Объект.ConnectionToExternalBaseBPathToFile;
		//Создание временного файла на сервере
		ФайлБ = ПолучитьИзВременногоХранилища(СтруктураПараметровОтКлиента.АдресВременногоХранилищаФайлаБ); 
		PathToFileBatServer = ПолучитьИмяВременногоФайла(Объект.ConnectionToExternalBaseBFileFormat);
		ФайлБ.Записать(PathToFileBatServer);
		Объект.ConnectionToExternalBaseBPathToFile = PathToFileBatServer;
	КонецЕсли;
	
	//Сравнение
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.RefreshDataPeriod();
	ОбработкаОбъект.CompareDataOnServer(ТекстОшибок);
	ПредставленияЗаголовковРеквизитов = ОбработкаОбъект.ПредставленияЗаголовковРеквизитов;
	ЧислоРеквизитов = ОбработкаОбъект.ЧислоРеквизитов;
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Для СчетчикРеквизитов = 1 По ЧислоРеквизитов Цикл 
		Элементы["ResultAttributeА" + СчетчикРеквизитов].Title = ПредставленияЗаголовковРеквизитов["А" + СчетчикРеквизитов];
		Элементы["ResultAttributeБ" + СчетчикРеквизитов].Title = ПредставленияЗаголовковРеквизитов["Б" + СчетчикРеквизитов];
	КонецЦикла;
	
	//Если источник А - файл, хранящийся на клиентском компьютере
	Если Объект.BaseTypeA = 3 И Объект.ConnectingToExternalBaseADeviceStorageFile = 1 Тогда
		//Удаление временного файла на сервере
		Попытка
			УдалитьФайлы(Объект.ConnectionToExternalBaseAPathToFile);
		Исключение КонецПопытки;
		//Восстановление пути к исходному файлу
		Объект.ConnectionToExternalBaseAPathToFile = PathToFileAatKlient;
	КонецЕсли;
	
	//Если источник Б - файл, хранящийся на клиентском компьютере
	Если Объект.BaseTypeB = 3 И Объект.ConnectingToExternalBaseBDeviceStorageFile = 1 Тогда
		//Удаление временного файла на сервере
		Попытка
			УдалитьФайлы(Объект.ConnectionToExternalBaseBPathToFile);
		Исключение КонецПопытки;
		//Восстановление пути к исходному файлу
		Объект.ConnectionToExternalBaseBPathToFile = PathToFileBatKlient;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиенте()
	
	ТекстОшибок = "";
	СтруктураПараметровНаКлиенте = Новый Структура;
	СтруктураПараметровНаКлиенте.Вставить("АдресВременногоХранилищаФайлаА", "");
	СтруктураПараметровНаКлиенте.Вставить("АдресВременногоХранилищаФайлаБ", "");
	
	СравнитьДанныеНаКлиентеПередатьФайлА(СтруктураПараметровНаКлиенте, ТекстОшибок);
		
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиентеПередатьФайлА(СтруктураПараметровНаКлиенте, ТекстОшибок)
	
	//Передача файла А с клиента на сервер
	Если Объект.BaseTypeA = 3 И Объект.ConnectingToExternalBaseADeviceStorageFile = 1 Тогда
		АдресВременногоХранилищаФайлаА = "";
		НачатьПомещениеФайла(Новый ОписаниеОповещения("СравнитьДанныеНаКлиентеПередатьФайлАЗавершение", ЭтаФорма, Новый Структура("СтруктураПараметровНаКлиенте, ТекстОшибок", СтруктураПараметровНаКлиенте, ТекстОшибок)), АдресВременногоХранилищаФайлаА,Объект.ConnectionToExternalBaseAPathToFile,Ложь);
	Иначе
		СравнитьДанныеНаКлиентеПередатьФайлБ(СтруктураПараметровНаКлиенте, ТекстОшибок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиентеПередатьФайлАЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	СтруктураПараметровНаКлиенте = ДополнительныеПараметры.СтруктураПараметровНаКлиенте;
	ТекстОшибок = ДополнительныеПараметры.ТекстОшибок;

	Если Результат Тогда
		СтруктураПараметровНаКлиенте.АдресВременногоХранилищаФайлаА = Адрес;
		СравнитьДанныеНаКлиентеПередатьФайлБ(СтруктураПараметровНаКлиенте, ТекстОшибок);
	Иначе
		ТекстОшибок = "Не удалось поместить во временное хранилище файл А: """ + Объект.ConnectionToExternalBaseAPathToFile + """";
		Сообщить(ТекстОшибок);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиентеПередатьФайлБ(СтруктураПараметровНаКлиенте, ТекстОшибок)
	
	//Передача файла Б с клиента на сервер
	Если Объект.BaseTypeB = 3 И Объект.ConnectingToExternalBaseBDeviceStorageFile = 1 Тогда
		АдресВременногоХранилищаФайлаБ = "";
		НачатьПомещениеФайла(Новый ОписаниеОповещения("СравнитьДанныеНаКлиентеПередатьФайлБЗавершение", ЭтаФорма, Новый Структура("СтруктураПараметровНаКлиенте, ТекстОшибок", СтруктураПараметровНаКлиенте, ТекстОшибок)), АдресВременногоХранилищаФайлаБ,Объект.ConnectionToExternalBaseBPathToFile,Ложь);
		Возврат;
	Иначе
		СравнитьДанныеНаКлиентеЗавершение(СтруктураПараметровНаКлиенте, ТекстОшибок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиентеПередатьФайлБЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	СтруктураПараметровНаКлиенте = ДополнительныеПараметры.СтруктураПараметровНаКлиенте;
	ТекстОшибок = ДополнительныеПараметры.ТекстОшибок;
	
	Если Результат Тогда
		СтруктураПараметровНаКлиенте.АдресВременногоХранилищаФайлаБ = Адрес;			
	Иначе
		ТекстОшибок = "Не удалось поместить во временное хранилище файл Б: """ + Объект.ConnectionToExternalBaseBPathToFile + """";
		Сообщить(ТекстОшибок);
		Возврат;
	КонецЕсли;
	
	СравнитьДанныеНаКлиентеЗавершение(СтруктураПараметровНаКлиенте, ТекстОшибок);

КонецПроцедуры

&НаКлиенте
Процедура СравнитьДанныеНаКлиентеЗавершение(СтруктураПараметровНаКлиенте, ТекстОшибок)
	
	CompareDataOnServer(СтруктураПараметровНаКлиенте, ТекстОшибок);
	Если Не ПустаяСтрока(ТекстОшибок) Тогда
		Сообщить(ТекстОшибок);
	КонецЕсли; 
	
	Элементы.ГруппаОсновная.ТекущаяСтраница = Элементы.ГруппаРезультатСравнения;
	ОбновитьВидимостьДоступностьЭлементовФормы();

КонецПроцедуры

&НаСервере
Процедура RefreshDataPeriod()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.RefreshDataPeriod();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
			
КонецПроцедуры
#КонецОбласти 

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытиеФормыПодтверждено = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеВВидеСтруктуры(СохранятьТабличныеДокументы)
	
	ФормаОбъект = РеквизитФормыВЗначение("Объект");
	ДанныеСтруктура = ФормаОбъект.GetDataAsStructureOnServer(СохранятьТабличныеДокументы);
	ЗначениеВРеквизитФормы(ФормаОбъект, "Объект");
	
	Возврат ДанныеСтруктура;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьКонструкторЗапроса(BaseID)    	
	
	QueryText = Объект["QueryText" + BaseID];
		
	Если Объект["BaseType" + BaseID] = 0 Тогда
		
		Если ЗначениеЗаполнено(QueryText) Тогда
			Конструктор = Новый КонструкторЗапроса(QueryText);
		Иначе
			Конструктор = Новый КонструкторЗапроса();
		КонецЕсли;
		
		#Если ТолстыйКлиентУправляемоеПриложение Тогда
			Если Конструктор.ОткрытьМодально() Тогда
				Объект["QueryText" + BaseID] = Конструктор.Текст;
			КонецЕсли;
		#ИначеЕсли ТонкийКлиент Тогда
			ПараметрыКонструктора = Новый Структура("Конструктор, BaseID", Конструктор, BaseID);
			ОповещениеКонструктора = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияКонструктора", ЭтаФорма, ПараметрыКонструктора);
			Конструктор.Показать(ОповещениеКонструктора);
		#КонецЕсли
		
	ИначеЕсли Объект["BaseType" + BaseID] = 1 Тогда
		
		Если Объект["WorkOptionExternalBase" + BaseID] = 0 Тогда
			ParameterConnections = 
				"File=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "ПутьКБазе"]
				+ """;Usr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Логин"]
				+ """;Pwd=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Пароль"] + """;";	
		Иначе
			ParameterConnections = 
				"Srvr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Сервер"]
				+ """;Ref=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "ПутьКБазе"] 
				+ """;Usr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Логин"] 
				+ """;Pwd=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Пароль"] + """;";
		КонецЕсли;

		
		Попытка
			Application = Новый COMОбъект(СтрЗаменить(Объект["VersionPlatformExternalBase" + BaseID],".","") + ".Application");
			Подключение = Application.Connect(ParameterConnections);
		Исключение
			Сообщить("Ошибка при подключении к внешней базе: " + ОписаниеОшибки());
			Возврат;
		КонецПопытки;
			
		Если Подключение Тогда
			Конструктор = Application.NewObject("КонструкторЗапроса");
			Конструктор.Текст = Объект["QueryText" + BaseID];
			Если Конструктор.ОткрытьМодально() Тогда
				Объект["QueryText" + BaseID] = Конструктор.Текст;
			КонецЕсли;
		КонецЕсли;
	 	
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияКонструктора(Результат, ПараметрыКонструктора) Экспорт
	
	Если Не ПустаяСтрока(Результат) Тогда
		Объект["QueryText" + ПараметрыКонструктора.BaseID] = СокрЛП(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКодДляВыводаИЗапретаВыводаСтрок()
	
	Если Не Объект.CodeForOutputRowsEditedManually Тогда
		
		Если Объект.ConditionsOutputRows.Count() = 0 Тогда
			
			Объект.CodeForOutputRows = "Истина";
			
		Иначе
			
			Объект.CodeForOutputRows = "";
		
			Для Каждого СтрокаТЧ Из Объект.ConditionsOutputRows Цикл
				
				КодИзСтрокиТЧ = ПреобразоватьСтрокуРеквизитовВКодДляВыводаИЗапретаВыводаСтрок(СтрокаТЧ);
				Объект.CodeForOutputRows =
					Объект.CodeForOutputRows 
					+ ?(ПустаяСтрока(Объект.CodeForOutputRows), "", Символы.ПС + Объект.BooleanOperatorForConditionsOutputRows + " ")
					+ КодИзСтрокиТЧ;
				
			КонецЦикла;
				
		КонецЕсли;
		
		Объект.CodeForOutputRows = "УсловияВыводаСтрокиВыполнены = " + Объект.CodeForOutputRows + ";";
		
	КонецЕсли;
	
	Если Не Объект.CodeForProhibitingOutputRowsEditedManually Тогда
		
		Если Объект.ConditionsProhibitOutputRows.Count() = 0 Тогда
			
			Объект.CodeForProhibitingOutputRows = "Ложь";
			
		Иначе
			
			Объект.CodeForProhibitingOutputRows = "";
		
			Для Каждого СтрокаТЧ Из Объект.ConditionsProhibitOutputRows Цикл
				
				КодИзСтрокиТЧ = ПреобразоватьСтрокуРеквизитовВКодДляВыводаИЗапретаВыводаСтрок(СтрокаТЧ);
				Объект.CodeForProhibitingOutputRows =
					Объект.CodeForProhibitingOutputRows 
					+ ?(ПустаяСтрока(Объект.CodeForProhibitingOutputRows), "", Символы.ПС + Объект.BooleanOperatorForProhibitingConditionsOutputRows + " ")
					+ КодИзСтрокиТЧ;
				
			КонецЦикла;
				
		КонецЕсли;
		
		Объект.CodeForProhibitingOutputRows = "УсловияЗапретаВыводаСтрокиВыполнены = " + Объект.CodeForProhibitingOutputRows + ";";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПреобразоватьСтрокуРеквизитовВКодДляВыводаИЗапретаВыводаСтрок(СтрокаТЧ)

	КодИзСтрокиТЧ = "";
	
	Если СтрокаТЧ.Condition <> "Заполнен" Тогда					
					
		Если СтрокаТЧ.ComparisonType = "Значение" Тогда
			Если ТипЗнч(СтрокаТЧ.ComparedValue) = Тип("Дата") Тогда 
				ПраваяСторона = 
					"Дата("
					+ Год(СтрокаТЧ.ComparedValue)
					+ ","
					+ Месяц(СтрокаТЧ.ComparedValue)
					+ ","
					+ День(СтрокаТЧ.ComparedValue)
					+ ","
					+ Час(СтрокаТЧ.ComparedValue)
					+ ","
					+ Минута(СтрокаТЧ.ComparedValue)
					+ ","
					+ Секунда(СтрокаТЧ.ComparedValue)
					+ ")";
			ИначеЕсли ТипЗнч(СтрокаТЧ.ComparedValue) = Тип("Число") Тогда 
				ПраваяСторона = Строка(СтрокаТЧ.ComparedValue);
			ИначеЕсли ТипЗнч(СтрокаТЧ.ComparedValue) = Тип("Строка") Тогда 
				ПраваяСторона = """" + Строка(СтрокаТЧ.ComparedValue) + """";
			ИначеЕсли ТипЗнч(СтрокаТЧ.ComparedValue) = Тип("Булево") Тогда 
				Если СтрокаТЧ.ComparedValue Тогда
					ПраваяСторона = "Истина";
				Иначе
					ПраваяСторона = "Ложь";
				КонецЕсли;
			Иначе
				ПраваяСторона = Строка(СтрокаТЧ.ComparedValue);
			КонецЕсли;
			
		Иначе
			ПраваяСторона = СтрокаТЧ.NameComparedAttribute2;
		КонецЕсли;
		
		КодИзСтрокиТЧ =
			СтрокаТЧ.NameComparedAttribute
			+ " "
			+ СтрокаТЧ.Condition
			+ " "
			+ ПраваяСторона;
		
	Иначе
		
		КодИзСтрокиТЧ =
			"ЗначениеЗаполнено("
			+ СтрокаТЧ.NameComparedAttribute
			+ ")";
		
	КонецЕсли;
	
	Возврат КодИзСтрокиТЧ;	

КонецФункции

&НаСервере
Процедура ПолучитьПараметрыИзЗапросаНаСервере(BaseID)
	
	Если ПустаяСтрока(Объект["QueryText" + BaseID]) Тогда	
		Возврат;
	КонецЕсли;
	
	//Текущая
	Если Объект["BaseType" + BaseID] = 0 Тогда
		
		Запрос = Новый Запрос();
		
	//Внешняя
	ИначеЕсли Объект["BaseType" + BaseID] = 1 Тогда
		
		Если Объект["WorkOptionExternalBase" + BaseID] = 0 Тогда
			ParameterConnections = 
				"File=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "ПутьКБазе"]
				+ """;Usr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Логин"]
				+ """;Pwd=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Пароль"] + """;";	
		Иначе
			ParameterConnections = 
				"Srvr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Сервер"]
				+ """;Ref=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "ПутьКБазе"] 
				+ """;Usr=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Логин"] 
				+ """;Pwd=""" + Объект["ПодключениеКВнешнейБазе" + BaseID + "Пароль"] + """;";
		КонецЕсли;
				
		Попытка
			COMConnector = Новый COMОбъект(Объект["VersionPlatformExternalBase" + BaseID] + ".COMConnector");
			Подключение = COMConnector.Connect(ParameterConnections);
		Исключение
			ТекстОшибки = "Ошибка при подключении к внешней базе: " + ОписаниеОшибки();
			Сообщить(ТекстОшибки);
			ТекстОшибок = ТекстОшибок + Символы.ПС + ТекстОшибки;
			Возврат;
		КонецПопытки;

		Запрос = Подключение.NewObject("Запрос");		
	
	КонецЕсли;
	
	Запрос.Текст = Объект["QueryText" + BaseID];
	
	Попытка
		ПараметрыЗапроса = Запрос.НайтиПараметры();
	Исключение
		Сообщить("Ошибка при получении списка параметров: " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		
		ИмяПараметра = ПараметрЗапроса.Имя;
		Если ИмяПараметра = "ДатаНачала" Или ИмяПараметра = "ДатаОкончания" Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныеПараметры = Объект["ParameterList" + BaseID].НайтиСтроки(Новый Структура("ParameterName", ИмяПараметра));
		Если НайденныеПараметры.Count() = 0 Тогда
			ТекущийПараметр = Объект["ParameterList" + BaseID].Добавить();
			ТекущийПараметр.ParameterName = ИмяПараметра;
		Иначе
			ТекущийПараметр = НайденныеПараметры[0];
		КонецЕсли; 
		
		ТекущийПараметр.ParameterValue = ПараметрЗапроса.ТипЗначения.ПривестиЗначение(ТекущийПараметр.ЗначениеПараметра);		
		ТекущийПараметр.ParameterType = Строка(ТипЗнч(ТекущийПараметр.ЗначениеПараметра));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТипыСтолбцовКлючаВоВсехСтроках()
	
	Для Каждого СтрокаТЗ Из Объект.Result Цикл
	
		СтрокаТЗ.ColumnType1Key = ТипЗнч(СтрокаТЗ.Key1);
		СтрокаТЗ.ColumnType2Key = ТипЗнч(СтрокаТЗ.Key2);
		СтрокаТЗ.ColumnType3Key = ТипЗнч(СтрокаТЗ.Key3);
	
	КонецЦикла; 
		
КонецПроцедуры

&НаСервере
Функция ВыгрузитьРезультатВФайлНаСервере(ДляКлиента = Ложь)
	
	РеквизитОбъект = РеквизитФормыВЗначение("Объект");
	АдресФайла = РеквизитОбъект.ВыгрузитьРезультатВФайлНаСервере(ДляКлиента);
	Возврат АдресФайла;
	
КонецФункции

&НаКлиенте
Процедура КомандаВыгрузитьРезультатВФайлНаКлиентеЗавершениеВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	АдресФайла = ВыгрузитьРезультатВФайлНаСервере(Истина);
	Если АдресФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогСохраненияФайла.ПолноеИмяФайла = Объект.PathToDownloadFile;
	ДиалогСохраненияФайла.Фильтр = "*." + Объект.UploadFileFormat + "|*." + Объект.UploadFileFormat;
	ДиалогСохраненияФайла.Title = "Выберите каталог"; 
	
	ДиалогСохраненияФайла.Показать(Новый ОписаниеОповещения("КомандаВыгрузитьРезультатВФайлНаКлиентеЗавершение", ЭтаФорма, Новый Структура("ДанныеФайла, ДиалогСохраненияФайла", ДанныеФайла, ДиалогСохраненияФайла)));

КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьРезультатВФайлНаКлиентеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДанныеФайла = ДополнительныеПараметры.ДанныеФайла;
	ДиалогСохраненияФайла = ДополнительныеПараметры.ДиалогСохраненияФайла;
	         	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		ДанныеФайла.Записать(ДиалогСохраненияФайла.ПолноеИмяФайла);
		Сообщить(Формат(ТекущаяДата(),"ДФ='yyyy.MM.dd HH.mm.ss'") + ": формирование файла завершено (" + ДиалогСохраненияФайла.ПолноеИмяФайла + ")");
		
	Иначе
		
		Сообщить(Формат(ТекущаяДата(),"ДФ='yyyy.MM.dd HH.mm.ss'") + ": формирование файла отменено");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере(BaseID, МаксимальноеЧислоСтрок = 0, ТолькоДубликаты = Ложь, Подключение = Неопределено)

	ТекстОшибки = "";
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		
	Если Не ОбработкаОбъект.CheckFillingAttributes(BaseID) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Подключение = Неопределено;
	ТЗ = ОбработкаОбъект.ReadDataAndGetValueTable(BaseID, ТекстОшибки, Подключение);
	
	Если ТЗ = Неопределено Тогда
		Сообщить(ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	
	Макет = ОбработкаОбъект.ПолучитьМакет("ФормаПредварительногоПросмотра");
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	//Key 1
	ИмяКлюча1 = ТЗ.Колонки.Get(0).Name;
	ColumnsWithKeyRow = ИмяКлюча1;
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка|Ключ1");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	//Key 2
	Если Объект.NumberColumnsInKey > 1 Тогда
		ИмяКлюча2 = ТЗ.Колонки.Get(1).Name;
		ColumnsWithKeyRow = ColumnsWithKeyRow + "," + ИмяКлюча2;
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка|Ключ2");
		ТабличныйДокумент.Присоединить(ОбластьШапка);
	КонецЕсли;
	
	//Key 3
	Если Объект.NumberColumnsInKey > 2 Тогда
		ИмяКлюча3 = ТЗ.Колонки.Get(2).Name;
		ColumnsWithKeyRow = ColumnsWithKeyRow + "," + ИмяКлюча3;
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка|Ключ3");
		ТабличныйДокумент.Присоединить(ОбластьШапка);
	КонецЕсли;
	
	ТЗ.Сортировать(ColumnsWithKeyRow);
	
	ТЗ_Сгруппированная = ТЗ.Скопировать(); 
	
	ColumnNameNumberOfRowsDataSource = "NumberOfRowsDataSource_" + СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
	ТЗ_Сгруппированная.Колонки.Добавить(ColumnNameNumberOfRowsDataSource);
	
	ОбластьРеквизиты = Макет.ПолучитьОбласть("Шапка|Реквизиты");
	ТабличныйДокумент.Присоединить(ОбластьРеквизиты);
		
	ТЗ_Сгруппированная.ЗаполнитьЗначения(1,ColumnNameNumberOfRowsDataSource);	
	ТЗ_Сгруппированная.Свернуть(ColumnsWithKeyRow, ColumnNameNumberOfRowsDataSource);
	ТЗ_Сгруппированная.Индексы.Добавить(ColumnsWithKeyRow);
		
	ЧислоКолонокТЗ = ТЗ.Колонки.Count();
	RowsCounter = 0;
	Для Каждого СтрокаТЗ Из ТЗ Цикл
		
		RowsCounter = RowsCounter + 1;
		
		Если МаксимальноеЧислоСтрок > 0 И RowsCounter > МаксимальноеЧислоСтрок Тогда
			Прервать;
		КонецЕсли;
		
		Если Подключение = Неопределено Тогда
			ОтборСтруктура = Новый Структура;
		Иначе
			ОтборСтруктура = Подключение.NewObject("Структура");
		КонецЕсли;
				
		Ключ1 = СтрокаТЗ.Get(0);
		ОтборСтруктура.Вставить(ИмяКлюча1, Ключ1);
		
		Если Объект.NumberColumnsInKey > 1 Тогда
			Ключ2 = СтрокаТЗ.Get(1);
			ОтборСтруктура.Вставить(ИмяКлюча2, Ключ2);
		Иначе
			Ключ2 = Неопределено;
		КонецЕсли;
		
		Если Объект.NumberColumnsInKey > 2 Тогда
			Ключ3 = СтрокаТЗ.Get(2);
			ОтборСтруктура.Вставить(ИмяКлюча3, Ключ3);
		Иначе
			Ключ3 = Неопределено;
		КонецЕсли;
		
		СтрокиСгруппированнойТЗ = ТЗ_Сгруппированная.FindRows(ОтборСтруктура);
		ЧислоСтрокПоКлючу = ?(СтрокиСгруппированнойТЗ.Count(), СтрокиСгруппированнойТЗ.Get(0)[ColumnNameNumberOfRowsDataSource], 0);
		Если ЧислоСтрокПоКлючу > 1 Тогда
			ИмяОбластиСтрока = "СтрокаСОшибками";
		Иначе
			Если ТолькоДубликаты Тогда
				Продолжить;
			КонецЕсли;
			ИмяОбластиСтрока = "СтрокаБезОшибок";
		КонецЕсли;
		
		ОбластьСтрока = Макет.ПолучитьОбласть(ИмяОбластиСтрока + "|Ключ1");
		ОбластьСтрока.Параметры.Ключ1 = Строка(Ключ1);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		Если Объект.NumberColumnsInKey > 1 Тогда
			ОбластьСтрока = Макет.ПолучитьОбласть(ИмяОбластиСтрока + "|Ключ2");
			ОбластьСтрока.Параметры.Ключ2 = Строка(Ключ2);
			ТабличныйДокумент.Присоединить(ОбластьСтрока);
		КонецЕсли;
		
		Если Объект.NumberColumnsInKey > 2 Тогда
			ОбластьСтрока = Макет.ПолучитьОбласть(ИмяОбластиСтрока + "|Ключ3");
			ОбластьСтрока.Параметры.Ключ3 = Строка(Ключ3);
			ТабличныйДокумент.Присоединить(ОбластьСтрока);
		КонецЕсли;		
		
		ОбластьСтрока = Макет.ПолучитьОбласть(ИмяОбластиСтрока + "|Реквизиты");
		ОбластьСтрока.Параметры.ЧислоСтрок = ЧислоСтрокПоКлючу; 
		
		ЧислоРеквизитов = 5;
		СмещениеНомераРеквизита = Объект.NumberColumnsInKey;
		Для ColumnCounter = 1 По Мин(ЧислоРеквизитов, ЧислоКолонокТЗ - Объект.NumberColumnsInKey) Цикл
			ОбластьСтрока.Параметры["Реквизит" + ColumnCounter] = Строка(СтрокаТЗ.Get(ColumnCounter + СмещениеНомераРеквизита - 1));
		КонецЦикла;
		
		ТабличныйДокумент.Присоединить(ОбластьСтрока);
			
	КонецЦикла;
	
	ТЗ = Неопределено;
	ТЗ_Сгруппированная = Неопределено;
	Подключение = Неопределено;
	
	ТабличныйДокумент.Защита = Ложь;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
		
КонецФункции


#Область Видимость_доступность_элементов_формы
&НаКлиенте
Процедура ОбновитьВидимостьДоступностьЭлементовФормы()
		
	ОбновитьВидимостьДоступностьЭлементовФормыПоИдентификаторуБазы("А");
	ОбновитьВидимостьДоступностьЭлементовФормыПоИдентификаторуБазы("Б");
	Элементы.ResultKey2.Видимость = Объект.NumberColumnsInKey > 1;
	Элементы.ResultKey3.Видимость = Объект.NumberColumnsInKey > 2;
	Элементы.ResultColumnType1Key.Видимость = Объект.DisplayKeyColumnTypes;
	Элементы.ResultColumnType2Key.Видимость = Объект.DisplayKeyColumnTypes И Объект.NumberColumnsInKey > 1;
	Элементы.ResultColumnType3Key.Видимость = Объект.DisplayKeyColumnTypes И Объект.NumberColumnsInKey > 2;
	Элементы.РезультатКомандаВидимостьТиповСтолбцовКлюча.Пометка = Объект.DisplayKeyColumnTypes;
	
	//Если Объект.PeriodTypeAbsolute Тогда
	Если Объект.PeriodType = 1 Тогда
		Элементы.AbsolutePeriodValue.ТолькоПросмотр = Истина;
		Элементы.ГруппаОтносительныйПериод.Видимость = Истина;
		Элементы.ГруппаПодчиненныйОтносительныйПериод.Видимость = Ложь;
	ИначеЕсли Объект.PeriodType = 2 Тогда	
		Элементы.AbsolutePeriodValue.ТолькоПросмотр = Истина;
		Элементы.ГруппаОтносительныйПериод.Видимость = Истина;
		Элементы.ГруппаПодчиненныйОтносительныйПериод.Видимость = Истина;
	Иначе
		Элементы.AbsolutePeriodValue.ТолькоПросмотр = Ложь;
		Элементы.ГруппаОтносительныйПериод.Видимость = Ложь;
	КонецЕсли;
	
	ОбновитьЗаголовок();
	ОбновитьРеквизитыПроизвольныйКод();
	ОбновитьВидимостьДоступностьВкладкиУсловияЗапретаВыводаСтрок();
	ОбновитьВидимостьДоступностьВкладкиУсловияВыводаСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРеквизитыПроизвольныйКод()
	
	Для СчетчикЭлементов = 1 По 3 Цикл
		ОбновитьВидимостьДоступностьРеквизитаПроизвольныйКод(СчетчикЭлементов, "А"); 
		ОбновитьВидимостьДоступностьРеквизитаПроизвольныйКод(СчетчикЭлементов, "Б");
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьРеквизитаПроизвольныйКод(НомерЭлементаФормы, BaseID)
	
	Элементы["ArbitraryKeyCode" + НомерЭлементаФормы + BaseID].Видимость = 
		Объект["ExecuteArbitraryKeyCode" + НомерЭлементаФормы + BaseID];
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовок()
	
	ЭтаФорма.Title = "КСД: " + Объект.Title;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьЭлементовФормыПоИдентификаторуБазы(BaseID)
	
	Элементы["ГруппаОбработкаКлюча2" + BaseID].Видимость = Объект.NumberColumnsInKey > 1;
	Элементы["ГруппаОбработкаКлюча3" + BaseID].Видимость = Объект.NumberColumnsInKey > 2;
	
	Элементы["ГруппаСтраницаПараметрыЗапроса" + BaseID].Видимость = Объект["BaseType" + BaseID] <= 1;
	Элементы["ParameterList"  + BaseID + "КомандаПолучитьПараметрыЗапроса"  + BaseID].Видимость = Объект["BaseType" + BaseID] <= 2;
	
	//Таблица 
	Элементы["ГруппаСтраницаТаблица" + BaseID].Видимость = Объект["BaseType" + BaseID] = 4;
		
	//1C 8 внешняя
	Если Объект["BaseType" + BaseID] = 1 Тогда
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Истина;
		Элементы["ГруппаВариантВерсияПлатформыБазы" + BaseID].Видимость 						= Истина;
		Если Объект["WorkOptionExternalBase" + BaseID] = 1 Тогда
			Элементы["ConnectionToExternalBase" + BaseID + "Server"].Видимость 				= Истина;
			Элементы["ConnectionToExternalBase" + BaseID + "PathBase"].Title 			= "Имя базы";
		Иначе
			Элементы["ConnectionToExternalBase" + BaseID + "Server"].Видимость 				= Ложь;
			Элементы["ConnectionToExternalBase" + BaseID + "PathBase"].Title 			= "Путь к базе";
		КонецЕсли;
		Элементы["ConnectionToExternalBase" + BaseID + "ДрайверSQL"].Видимость 				= Ложь;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Истина;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Title							= "Текст запроса";
		Элементы["ДекорацияТекстЗапроса" + BaseID].Видимость									= Истина;
		Элементы["ГруппаТекстЗапроса" + BaseID + "Команды"].Видимость 						= Истина;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Ложь;	
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Ложь;
		
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Ложь;
		
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Истина;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Истина;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Истина;
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Ложь;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Не Объект["UseAsKeyUniqueIdentifier" + BaseID];
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Не Объект["UseAsKey2UniqueIdentifier" + BaseID];
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Не Объект["UseAsKey3UniqueIdentifier" + BaseID];
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKeyUniqueIdentifier" + BaseID];
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKey2UniqueIdentifier" + BaseID];
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKey3UniqueIdentifier" + BaseID];
		Элементы["KeyLengthWhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKeyToString" + BaseID];
		Элементы["KeyLength2WhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKey2ToString" + BaseID];
		Элементы["KeyLength3WhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKey3ToString" + BaseID];
		
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Ложь;
							
	//SQL
	ИначеЕсли Объект["BaseType" + BaseID] = 2 Тогда
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Истина;
		Элементы["ГруппаВариантВерсияПлатформыБазы" + BaseID].Видимость 						= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "Server"].Видимость 					= Истина;
		Элементы["ConnectionToExternalBase" + BaseID + "PathBase"].Title 				= "Имя базы данных";
		Элементы["ConnectionToExternalBase" + BaseID + "ДрайверSQL"].Видимость 				= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Ложь;
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Ложь;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Истина;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Title							= "Текст запроса";
		Элементы["ДекорацияТекстЗапроса" + BaseID].Видимость									= Истина;
		Элементы["ГруппаТекстЗапроса" + BaseID + "Команды"].Видимость 						= Ложь;
		
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Ложь;
		
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Ложь;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Истина;
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Истина;
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Истина;
		//Приведение производится просто к строке без указания длины
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Ложь;
		
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Ложь;
				                                                                       						
	//Файл
	ИначеЕсли Объект["BaseType" + BaseID] = 3 Тогда
		
		ФайлФорматаXML = Объект["ConnectionToExternalBase" + BaseID + "FileFormat" ] = "XML";
		ФайлФорматаXLS = Объект["ConnectionToExternalBase" + BaseID + "FileFormat" ] = "XLS";
		ФайлФорматаDOC = Объект["ConnectionToExternalBase" + BaseID + "FileFormat" ] = "DOC";
				
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Ложь;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость							= Ложь;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлуОбщие" + BaseID].Видимость					= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлуXMLJSON" + BaseID].Видимость 				= ФайлФорматаXML;
		Элементы["ГруппаПараметрыПодключенияКФайлуXML" + BaseID].Видимость 					= ФайлФорматаXML;
		Элементы["ГруппаПараметрыПодключенияКФайлуНеXML" + BaseID].Видимость					= Не ФайлФорматаXML;
		Элементы["ConnectionToExternalBase" + BaseID + "NumberTableInFile"].Видимость		= ФайлФорматаXLS Или ФайлФорматаDOC;
		Элементы["ConnectionToExternalBase" + BaseID + "NumberTableInFile"].Title		= ?(ФайлФорматаXLS, "Номер книги", "Номер таблицы");
		
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Истина;		
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Истина;
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Истина;
		
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Не ФайлФорматаXML;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Не ФайлФорматаXML;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Не ФайлФорматаXML;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= ФайлФорматаXML;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= ФайлФорматаXML;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= ФайлФорматаXML;
				
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Не ФайлФорматаXML;
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Не ФайлФорматаXML;
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Не ФайлФорматаXML;
		//Приведение производится просто к строке без указания длины
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Ложь;
		
		Элементы["НастройкиФайла" + BaseID + "NumberColumn"].Видимость						= Не ФайлФорматаXML;
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= ФайлФорматаXML;
							
	//Таблица
	ИначеЕсли Объект["BaseType" + BaseID] = 4 Тогда
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Ложь;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Ложь;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлуОбщие" + BaseID].Видимость					= Ложь;
		Элементы["ГруппаПараметрыПодключенияКФайлуXMLJSON" + BaseID].Видимость				= Ложь;
		Элементы["ГруппаПараметрыПодключенияКФайлуXML" + BaseID].Видимость					= Ложь;
		Элементы["ГруппаПараметрыПодключенияКФайлуНеXML" + BaseID].Видимость					= Истина;
		Элементы["ConnectionToExternalBase" + BaseID + "NumberTableInFile"].Видимость		= Ложь;
		
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость							= Истина;
		
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Истина;
		
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Истина;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Истина;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Истина;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Ложь;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Ложь;
		
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Истина;
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Истина;
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Истина;
		//Приведение производится просто к строке без указания длины
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Ложь;
		
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Ложь;
		
	//1C 7.7 внешняя
	ИначеЕсли Объект["BaseType" + BaseID] = 5 Тогда
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Истина;
		Элементы["ГруппаВариантВерсияПлатформыБазы" + BaseID].Видимость 						= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "ДрайверSQL"].Видимость 				= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "Server"].Видимость 					= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "PathBase"].Title 				= "Путь к базе";
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Истина;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Title							= "Текст запроса";
		Элементы["ДекорацияТекстЗапроса" + BaseID].Видимость									= Истина;
		Элементы["ГруппаТекстЗапроса" + BaseID + "Команды"].Видимость 						= Ложь;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Ложь;
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Ложь;     		
		
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Ложь;
		
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Ложь;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Истина;
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Истина;
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Истина;
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Ложь;
		
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Ложь;

	//Строка JSON
	ИначеЕсли Объект["BaseType" + BaseID] = 6 Тогда
	
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Истина;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Title							= "Строка JSON";
		
		Элементы["ДекорацияТекстЗапроса" + BaseID].Видимость									= Ложь;
		Элементы["ГруппаТекстЗапроса" + BaseID + "Команды"].Видимость 						= Ложь;
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Ложь;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлуОбщие" + BaseID].Видимость					= Ложь;
		Элементы["ГруппаПараметрыПодключенияКФайлуXMLJSON" + BaseID].Видимость 				= Истина;
		Элементы["ГруппаПараметрыПодключенияКФайлуXML" + BaseID].Видимость 					= Ложь;
		Элементы["ГруппаПараметрыПодключенияКФайлуНеXML" + BaseID].Видимость					= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "NumberTableInFile"].Видимость		= Ложь;
		Элементы["ConnectionToExternalBase" + BaseID + "NumberTableInFile"].Title		= Ложь;
		
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Истина;		
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Истина;
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Истина;
		
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Истина;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Истина;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Истина;
				
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Ложь;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Ложь;
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Ложь;
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Ложь;
		//Приведение производится просто к строке без указания длины
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Ложь;
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Ложь;
		
		Элементы["НастройкиФайла" + BaseID + "NumberColumn"].Видимость						= Ложь;
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Истина;
							
	//1С 8 текущая
	Иначе 
		
		Элементы["ГруппаВариантПараметрыПодключенияКБазе" + BaseID].Видимость 				= Ложь;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Видимость 							= Истина;
		Элементы["ГруппаСтраницаТекстЗапроса" + BaseID].Title							= "Текст запроса";
		Элементы["ДекорацияТекстЗапроса" + BaseID].Видимость									= Истина;
		Элементы["ГруппаТекстЗапроса" + BaseID + "Команды"].Видимость 						= Истина;
		
		Элементы["ГруппаПараметрыПодключенияКФайлу" + BaseID].Видимость 						= Ложь;
		Элементы["ГруппаПараметрыКолонокФайла" + BaseID].Видимость 							= Ложь;
		
		Элементы["GroupCollapseTable" + BaseID].Видимость 								= Ложь;
		
		Элементы["ColumnNumberKeyFromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNumberKey3FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKeyFromFile" + BaseID].Видимость 								= Ложь;
		Элементы["ColumnNameKey2FromFile" + BaseID].Видимость 							= Ложь;
		Элементы["ColumnNameKey3FromFile" + BaseID].Видимость 							= Ложь;
		
		Элементы["UseAsKeyUniqueIdentifier" + BaseID].Видимость		= Истина;
		Элементы["UseAsKey2UniqueIdentifier" + BaseID].Видимость 	= Истина;
		Элементы["UseAsKey3UniqueIdentifier" + BaseID].Видимость 	= Истина;
		
		Элементы["CastKeyToString" + BaseID].Видимость 									= Не Объект["UseAsKeyUniqueIdentifier" + BaseID];
		Элементы["CastKey2ToString" + BaseID].Видимость 								= Не Объект["UseAsKey2UniqueIdentifier" + BaseID];
		Элементы["CastKey3ToString" + BaseID].Видимость 								= Не Объект["UseAsKey3UniqueIdentifier" + BaseID];
		Элементы["KeyLengthWhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKeyUniqueIdentifier" + BaseID];
		Элементы["KeyLength2WhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKey2UniqueIdentifier" + BaseID];
		Элементы["KeyLength3WhenCastingToString" + BaseID].Видимость 						= Не Объект["UseAsKey3UniqueIdentifier" + BaseID];
		Элементы["KeyLengthWhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKeyToString" + BaseID];
		Элементы["KeyLength2WhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKey2ToString" + BaseID];
		Элементы["KeyLength3WhenCastingToString" + BaseID].ТолькоПросмотр 					= Не Объект["CastKey3ToString" + BaseID];
		
		Элементы["НастройкиФайла" + BaseID + "ColumnName"].Видимость							= Ложь;
							
	КонецЕсли;
	
	Для Счетчик = 1 по 5 Цикл
		ОбновитьВидимостьРеквизитаТЧ("Реквизит" + BaseID + Счетчик);
	КонецЦикла;
				
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьРеквизитаТЧ(AttributeName)
	
	ВидимостьКолонки = Объект["Видимость" + AttributeName];
	Элементы["РезультатКомандаВидимость" + AttributeName].Пометка = ВидимостьКолонки;
	Элементы["Result" + AttributeName].Видимость = ВидимостьКолонки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация()
	
	Элементы.СравнитьДанные.Доступность = Объект.RelationalOperation > 0;
	Для СчетчикОпераций = 1 По 7 Цикл 
		
		Если СчетчикОпераций = Объект.RelationalOperation Тогда
			ЭтаФорма["Операция" + СчетчикОпераций] = ЭтаФорма["АктивнаяОперация" + СчетчикОпераций];			
		Иначе
			ЭтаФорма["Операция" + СчетчикОпераций] = ЭтаФорма["НеактивнаяОперация" + СчетчикОпераций];			
		КонецЕсли;
		
	КонецЦикла;		
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок()
	
	Элементы.BooleanOperatorForConditionsOutputRows.ТолькоПросмотр 		= Объект.CodeForOutputRowsEditedManually;
	Элементы.ConditionsOutputRows.ТолькоПросмотр 								= Объект.CodeForOutputRowsEditedManually;
	Элементы.BooleanOperatorForProhibitingConditionsOutputRows.ТолькоПросмотр 	= Объект.CodeForProhibitingOutputRowsEditedManually;
	Элементы.ConditionsProhibitOutputRows.ТолькоПросмотр 						= Объект.CodeForProhibitingOutputRowsEditedManually;
	
	Элементы.CodeForOutputRows.ТолькоПросмотр 								= Не Объект.CodeForOutputRowsEditedManually;
	Элементы.CodeForProhibitingOutputRows.ТолькоПросмотр 						= Не Объект.CodeForProhibitingOutputRowsEditedManually;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьВкладкиУсловияВыводаСтрок()
	
	Элементы.GroupConditionsOutputRows.ЦветФона = ?(Объект.ConditionsOutputRowsDisabled, WebЦвета.Розовый, ЦветФонаФормыПоУмолчанию);;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВидимостьДоступностьВкладкиУсловияЗапретаВыводаСтрок()
	
	Элементы.GroupConditionsProhibitOutputRows.ЦветФона = ?(Объект.ConditionsProhibitOutputRowsDisabled, WebЦвета.Розовый, ЦветФонаФормыПоУмолчанию);;
		
КонецПроцедуры
#КонецОбласти 


#Область Настройки

#Область Сохранение
&НаКлиенте
Процедура СохранитьНастройкиВФайлНаКлиенте(СохранятьТабличныеДокументы = Ложь)
	
	Режим = РежимДиалогаВыбораФайла.Сохранение;
	ДиалогВыбора = Новый ДиалогВыбораФайла(Режим);
	ДиалогВыбора.ПолноеИмяФайла = Объект.Title;
	Фильтр = "Файл xml (*.xml)|*.xml";
	ДиалогВыбора.Фильтр = Фильтр;
	ДиалогВыбора.Title = "Укажите файл для сохранения настроек";   

	ДиалогВыбора.Показать(Новый ОписаниеОповещения("СохранитьНастройкиВФайлНаКлиентеЗавершение", ЭтаФорма, Новый Структура("ДиалогВыбора,СохранятьТабличныеДокументы", ДиалогВыбора, СохранятьТабличныеДокументы)));
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайлНаКлиентеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбора = ДополнительныеПараметры.ДиалогВыбора;
	СохранятьТабличныеДокументы = ДополнительныеПараметры.СохранятьТабличныеДокументы;
	                             	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.Title = Сред(ДиалогВыбора.ПолноеИмяФайла, СтрНайти(ДиалогВыбора.ПолноеИмяФайла, "\", НаправлениеПоиска.СКонца) + 1);
		Адрес = СохранитьНастройкиВФайлНаСервере(СохранятьТабличныеДокументы);
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
		ДвоичныеДанные.Записать(ДиалогВыбора.ПолноеИмяФайла);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВБазуНаКлиенте(СохранятьТабличныеДокументы = Ложь);
	
	Если ЗначениеЗаполнено(Объект.RelatedDataComparisonOperation)  Тогда
	
		ПоказатьВопрос(Новый ОписаниеОповещения("СохранитьВСвязаннуюОперациюЗавершение", ЭтотОбъект, Новый Структура("ВыбратьЭлементСправочникаДляСохранения,СохранятьТабличныеДокументы",Истина,СохранятьТабличныеДокументы)), "Обновить элемент справочника """ + Объект.RelatedDataComparisonOperation + """?", РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОткрытьФормуВыбораОперацииДляЗаписи(СохранятьТабличныеДокументы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВСвязаннуюОперациюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	СохранятьТабличныеДокументы = ДополнительныеПараметры.СохранятьТабличныеДокументы;
	ВыбратьЭлементСправочникаДляСохранения = ДополнительныеПараметры.ВыбратьЭлементСправочникаДляСохранения;
	ПриЗакрытииФормы = ДополнительныеПараметры.Свойство("ПриЗакрытииФормы") И ДополнительныеПараметры.ПриЗакрытииФормы;
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		СохранитьНастройкиВБазуНаСервере(Объект.RelatedDataComparisonOperation, СохранятьТабличныеДокументы);
		ОбновитьЗаголовок();
		
	//Нажатие кнопки Сохранить в базу
	ИначеЕсли ВыбратьЭлементСправочникаДляСохранения = Истина И ПриЗакрытииФормы = Ложь Тогда
		
		ОткрытьФормуВыбораОперацииДляЗаписи();
				
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СохранитьВВыбраннуюОперациюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СохранятьТабличныеДокументы = ДополнительныеПараметры.СохранятьТабличныеДокументы;
	
	ВыбранныйЭлемент = Результат;
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		СохранитьНастройкиВБазуНаСервере(ВыбранныйЭлемент, СохранятьТабличныеДокументы);
		ОбновитьЗаголовок();
				
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиВФайлНаСервере(СохранятьТабличныеДокументы)
	
	PathToFile = ПолучитьИмяВременногоФайла("xml");
	Данные = ПолучитьДанныеВВидеСтруктуры(СохранятьТабличныеДокументы); 
	ХранилищеВнешнее = Новый ХранилищеЗначения(Данные);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(PathToFile, "UTF-8");
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ХранилищеВнешнее);
	ЗаписьXML.Закрыть();
	Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(PathToFile));
	УдалитьФайлы(PathToFile);
	
	Возврат Адрес;
			
КонецФункции

&НаСервере
Процедура СохранитьНастройкиВБазуНаСервере(ВыбранныйЭлемент, СохранятьТабличныеДокументы = Ложь)

	ФормаОбъект = РеквизитФормыВЗначение("Объект");
	ФормаОбъект.СохранитьНастройкиВБазуНаСервере(ВыбранныйЭлемент, СохранятьТабличныеДокументы);
	ЗначениеВРеквизитФормы(ФормаОбъект, "Объект");
		
КонецПроцедуры
#КонецОбласти 


#Область Загрузка
&НаКлиенте
Процедура ОткрытьНастройкиИзФайлаНаКлиенте(Знач Оповещение, ЗагружатьТабличныеДокументы = Ложь)

	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогВыбора = Новый ДиалогВыбораФайла(Режим);
	ДиалогВыбора.ПолноеИмяФайла = "";
	Фильтр = "Файл xml (*.xml)|*.xml";
	ДиалогВыбора.Фильтр = Фильтр;
	ДиалогВыбора.Title = "Укажите файл с настройками";   

	ДиалогВыбора.Показать(Новый ОписаниеОповещения("ОткрытьНастройкиИзФайлаНаКлиентеЗавершение", ЭтаФорма, Новый Структура("ДиалогВыбора, Оповещение, ЗагружатьТабличныеДокументы", ДиалогВыбора, Оповещение, ЗагружатьТабличныеДокументы)));

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиИзФайлаНаКлиентеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбора = ДополнительныеПараметры.ДиалогВыбора;
	Оповещение = ДополнительныеПараметры.Оповещение;	
	ЗагружатьТабличныеДокументы = ДополнительныеПараметры.ЗагружатьТабличныеДокументы;	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ДиалогВыбора.ПолноеИмяФайла));
		ОткрытьНастройкиИзФайлаНаСервере(Адрес, ЗагружатьТабличныеДокументы);
		ПервыйСимвол = СтрНайти(ДиалогВыбора.ПолноеИмяФайла, "\", НаправлениеПоиска.СКонца) + 1;
		ПоследнийСимвол = СтрНайти(ДиалогВыбора.ПолноеИмяФайла, ".", НаправлениеПоиска.СКонца);
		Объект.Title = Сред(ДиалогВыбора.ПолноеИмяФайла, ПервыйСимвол, ПоследнийСимвол - ПервыйСимвол);
		ОбновитьВидимостьДоступностьЭлементовФормы();
		ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация();
		ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Оповещение);

КонецПроцедуры

&НаСервере
Процедура ОткрытьНастройкиИзФайлаНаСервере(Адрес, ЗагружатьТабличныеДокументы = Ложь)
	
	PathToFile = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ДвоичныеДанные.Записать(PathToFile);
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(PathToFile,,,"UTF-8");
	ХранилищеВнешнее = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	Данные = ХранилищеВнешнее.Получить();
	ЗаполнитьЗначенияСвойств(Объект, Данные);
	
	Если Данные.Свойство("ТЗУсловияВыводаСтрок") Тогда
		Объект.ConditionsOutputRows.Загрузить(Данные.ТЗУсловияВыводаСтрок);
	Иначе
		Объект.ConditionsOutputRows.Очистить();
	КонецЕсли;
	
	Если Данные.Свойство("ТЗУсловияЗапретаВыводаСтрок") Тогда
		Объект.ConditionsProhibitOutputRows.Загрузить(Данные.ТЗУсловияЗапретаВыводаСтрок);
	Иначе
		Объект.ConditionsProhibitOutputRows.Очистить();
	КонецЕсли;
	
	Если Данные.Свойство("ValueTableSettingsFileA") Тогда
		Объект.SettingsFileA.Загрузить(Данные.ValueTableSettingsFileA);
	Иначе
		Объект.SettingsFileA.Очистить();
	КонецЕсли;
	
	Если Данные.Свойство("ValueTableSettingsFileB") Тогда
		Объект.SettingsFileB.Загрузить(Данные.ValueTableSettingsFileB);
	Иначе
		Объект.SettingsFileB.Очистить();
	КонецЕсли;
	
	Если Данные.Свойство("ValueTableParameterListA") Тогда
		Объект.ParameterListA.Загрузить(Данные.ValueTableParameterListA);
	Иначе
		Объект.ParameterListA.Очистить();
	КонецЕсли;
	
	Если Данные.Свойство("ValueTableParameterListB") Тогда
		Объект.ParameterListB.Загрузить(Данные.ValueTableParameterListB);
	Иначе
		Объект.ParameterListB.Очистить();
	КонецЕсли;
	
	Если ЗагружатьТабличныеДокументы Тогда
		Если Объект.BaseTypeA = 4 Тогда
			Попытка
				Если Данные.Свойство("TableAValueStorage") Тогда
					Объект.TableA = Данные.TableAValueStorage.Получить();
				КонецЕсли;
			Исключение
			КонецПопытки; 
		КонецЕсли;
		
		Если Объект.BaseTypeB = 4 Тогда
			Попытка
				Если Данные.Свойство("TableBValueStorage") Тогда
					Объект.TableB = Данные.TableBValueStorage.Получить();
				КонецЕсли;
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	ЧтениеXML.Закрыть();
	УдалитьФайлы(PathToFile);
	
	Объект.Result.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьНастройкиИзБазыНаСервере(ВыбранныйЭлемент, ЗагружатьТабличныеДокументы = Ложь)
	
	ФормаОбъект = РеквизитФормыВЗначение("Объект");
	ФормаОбъект.ОткрытьНастройкиИзБазыНаСервере(ВыбранныйЭлемент, ЗагружатьТабличныеДокументы);
	ЗначениеВРеквизитФормы(ФормаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиИзФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьВидимостьДоступностьЭлементовФормы();
	ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация();
	ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораОперацииДляЗаписи(СохранятьТабличныеДокументы = Ложь)

	ВыбранныйЭлемент = Неопределено;
	ОткрытьФорму("Справочник.ВС_ОперацииСравненияДанных.ФормаВыбора",,,,,, Новый ОписаниеОповещения("СохранитьВВыбраннуюОперациюЗавершение", ЭтаФорма, Новый Структура("СохранятьТабличныеДокументы",СохранятьТабличныеДокументы)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиИзБазыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныйЭлемент = Результат;
	ЗагружатьТабличныеДокументы = ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ЗагружатьТабличныеДокументы") И ДополнительныеПараметры.ЗагружатьТабличныеДокументы;
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		
		ОткрытьНастройкиИзБазыНаСервере(ВыбранныйЭлемент, ЗагружатьТабличныеДокументы);
		ОбновитьВидимостьДоступностьЭлементовФормы();
		ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация();
		ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();
		
	КонецЕсли;

КонецПроцедуры
#КонецОбласти 

#КонецОбласти 


#Область Обработка_выбора_параметра
&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметра(BaseID, СтандартнаяОбработка)
	
	Если Объект["BaseType" + BaseID] = 1 Тогда
		СтандартнаяОбработка = Ложь;
		СписокДоступныхТипов = Новый СписокЗначений;
		СписокДоступныхТипов.Добавить("Число");
		СписокДоступныхТипов.Добавить("Строка");
		СписокДоступныхТипов.Добавить("Дата");
		СписокДоступныхТипов.Добавить("Булево");
		ВыбранныйТип = Неопределено;

		ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПриНачалеВыбораЗначенияПараметраЗавершение4", ЭтаФорма, Новый Структура("BaseID", BaseID)), СписокДоступныхТипов, Элементы["ParameterList" + BaseID + "ParameterValue"]);
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраЗавершение4(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	BaseID = ДополнительныеПараметры.BaseID;
	
	
	ВыбранныйТип = ВыбранныйЭлемент;
	Если ВыбранныйТип = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы["ParameterList" + BaseID].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ТекущееЗначениеПараметра = Неопределено; 
	Иначе
		ТекущееЗначениеПараметра =  ТекущиеДанные.ParameterValue;
	КонецЕсли;
	
	Если ВыбранныйТип.Значение = "Число" Тогда
		ПоказатьВводЧисла(Новый ОписаниеОповещения("ПриНачалеВыбораЗначенияПараметраЗавершение3", ЭтаФорма, Новый Структура("ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра", ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра)), ТекущееЗначениеПараметра);
		Возврат;
	ИначеЕсли ВыбранныйТип.Значение = "Строка" Тогда
		ПоказатьВводСтроки(Новый ОписаниеОповещения("ПриНачалеВыбораЗначенияПараметраЗавершение2", ЭтаФорма, Новый Структура("ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра", ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра)), ТекущееЗначениеПараметра);
		Возврат;
	ИначеЕсли ВыбранныйТип.Значение = "Дата" Тогда
		ПоказатьВводДаты(Новый ОписаниеОповещения("ПриНачалеВыбораЗначенияПараметраЗавершение1", ЭтаФорма, Новый Структура("ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра", ВыбранныйТип, ТекущиеДанные, ТекущееЗначениеПараметра)), ТекущееЗначениеПараметра);
		Возврат;
	ИначеЕсли ВыбранныйТип.Значение = "Булево" Тогда
		ПоказатьВводЗначения(Новый ОписаниеОповещения("ПриНачалеВыбораЗначенияПараметраЗавершение", ЭтаФорма, Новый Структура("ТекущиеДанные, ТекущееЗначениеПараметра", ТекущиеДанные, ТекущееЗначениеПараметра)), ТекущееЗначениеПараметра,,Новый ОписаниеТипов("Булево"));
		Возврат;
	КонецЕсли;
	
	ПриНачалеВыбораЗначенияПараметраФрагмент3(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраЗавершение3(Число, ДополнительныеПараметры) Экспорт
	
	ВыбранныйТип = ДополнительныеПараметры.ВыбранныйТип;
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	ТекущееЗначениеПараметра = ?(Число = Неопределено, ДополнительныеПараметры.ТекущееЗначениеПараметра, Число);
	
	
	Если Не (Число <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	ПриНачалеВыбораЗначенияПараметраФрагмент3(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраФрагмент3(Знач ТекущееЗначениеПараметра, Знач ТекущиеДанные)
	
	ПриНачалеВыбораЗначенияПараметраФрагмент2(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраЗавершение2(Строка, ДополнительныеПараметры) Экспорт
	
	ВыбранныйТип = ДополнительныеПараметры.ВыбранныйТип;
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	ТекущееЗначениеПараметра = ?(Строка = Неопределено, ДополнительныеПараметры.ТекущееЗначениеПараметра, Строка);
	
	
	Если Не (Строка <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	ПриНачалеВыбораЗначенияПараметраФрагмент2(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраФрагмент2(Знач ТекущееЗначениеПараметра, Знач ТекущиеДанные)
	
	ПриНачалеВыбораЗначенияПараметраФрагмент1(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраЗавершение1(Дата, ДополнительныеПараметры) Экспорт
	
	ВыбранныйТип = ДополнительныеПараметры.ВыбранныйТип;
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	ТекущееЗначениеПараметра = ?(Дата = Неопределено, ДополнительныеПараметры.ТекущееЗначениеПараметра, Дата);
	
	
	Если Не (Дата <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	ПриНачалеВыбораЗначенияПараметраФрагмент1(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраФрагмент1(Знач ТекущееЗначениеПараметра, Знач ТекущиеДанные)
	
	ПриНачалеВыбораЗначенияПараметраФрагмент(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	ТекущееЗначениеПараметра = ?(Значение = Неопределено, ДополнительныеПараметры.ТекущееЗначениеПараметра, Значение);
	
	
	Если Не (Значение <> Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	ПриНачалеВыбораЗначенияПараметраФрагмент(ТекущееЗначениеПараметра, ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеВыбораЗначенияПараметраФрагмент(Знач ТекущееЗначениеПараметра, Знач ТекущиеДанные)
	
	ТекущиеДанные.ParameterValue = ТекущееЗначениеПараметра;

КонецПроцедуры
#КонецОбласти 

#КонецОбласти


#Область Команды
&НаКлиенте
Процедура СравнитьДанные(Команда)
	
	СравнитьДанныеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапросаБ(Команда)
	ОткрытьКонструкторЗапроса("Б");
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапросаА(Команда)
	ОткрытьКонструкторЗапроса("А");
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайл(Команда)
	
	СохранитьНастройкиВФайлНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиИТабличныеДокументыВФайл(Команда)
	
	СохранитьНастройкиВФайлНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВБазу(Команда)
	
	СохранитьНастройкиВБазуНаКлиенте();
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиИТабличныеДокументыВБазу(Команда)
	
	СохранитьНастройкиВБазуНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиИзФайла(Команда)
	
	ОткрытьНастройкиИзФайлаНаКлиенте(Новый ОписаниеОповещения("ОткрытьНастройкиИзФайлаЗавершение", ЭтаФорма));
			
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиИТабличныеДокументыИзФайла(Команда)
	
	ОткрытьНастройкиИзФайлаНаКлиенте(Новый ОписаниеОповещения("ОткрытьНастройкиИзФайлаЗавершение", ЭтаФорма), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиИзБазы(Команда)
	
	ВыбранныйЭлемент = Неопределено; 
	
	ОткрытьФорму("Справочник.ВС_ОперацииСравненияДанных.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ОткрытьНастройкиИзБазыЗавершение", ЭтаФорма), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиИТабличныеДокументыИзБазы(Команда)
	
	ВыбранныйЭлемент = Неопределено; 
	
	ОткрытьФорму("Справочник.ВС_ОперацииСравненияДанных.ФормаВыбора",,,,,, Новый ОписаниеОповещения("ОткрытьНастройкиИзБазыЗавершение", ЭтаФорма, Новый Структура("ЗагружатьТабличныеДокументы", Истина)), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьПараметрыЗапросаА(Команда)
	
	ПолучитьПараметрыИзЗапросаНаСервере("А");
	Элементы.ГруппаСтраницыБазаА.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыЗапросаА;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьПараметрыЗапросаБ(Команда)
	
	ПолучитьПараметрыИзЗапросаНаСервере("Б");
	Элементы.ГруппаСтраницыБазаБ.ТекущаяСтраница = Элементы.ГруппаСтраницаПараметрыЗапросаБ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПосетитьСтраницуАвтора(Команда)

	НачатьЗапускПриложения(Новый ОписаниеОповещения("ПосетитьСтраницу", ЭтаФорма), "http://sertakov.by");
	
КонецПроцедуры

&НаКлиенте
Процедура ПосетитьСтраницуОбработки(Команда)
	
	НачатьЗапускПриложения(Новый ОписаниеОповещения("ПосетитьСтраницу", ЭтаФорма), "https://infostart.ru/public/581794/");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСкачатьОбработку(Команда)
	
	НачатьЗапускПриложения(Новый ОписаниеОповещения("ПосетитьСтраницу", ЭтаФорма), "http://sertakov.by/work/KSD.epf");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаАВсеСтроки(Команда)	
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("А");
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник А");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаА100Строк(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("А",100);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник А (100 строк)");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаБ100Строк(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("Б",100);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник Б (100 строк)");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаБВсеСтроки(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("Б");
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник Б");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаАДубликаты(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("А",,Истина);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник А (дубликаты)");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаБДубликаты(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("Б",,Истина);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник Б (дубликаты)");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаА1000Строк(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("А",1000);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник А (1000 строк)");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПредварительныйПросмотрИсточникаБ1000Строк(Команда)
	ТабличныйДокумент = ПолучитьТабличныйДокументСДаннымиИзИсточникаНаСервере("Б",1000);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать("Источник Б (1000 строк)");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область Обработчики_событий
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЦветФонаФормыПоУмолчанию = ЦветаСтиля.ЦветФонаФормы;
	
	Если Параметры.Свойство("UserMode") И Параметры.UserMode Тогда
		Объект.UserMode = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ОперацияСравненияДанных") И ЗначениеЗаполнено(Параметры.ОперацияСравненияДанных) Тогда
		
		Объект.RelatedDataComparisonOperation = Параметры.ОперацияСравненияДанных;
		ФормаОбъект = РеквизитФормыВЗначение("Объект");
		ФормаОбъект.ОткрытьНастройкиИзБазыНаСервере(Объект.RelatedDataComparisonOperation);
		ЗначениеВРеквизитФормы(ФормаОбъект, "Объект");
				
	Иначе 
		
		Объект.VersionPlatformExternalBaseA = "V83";
		Объект.VersionPlatformExternalBaseB = "V83";
		
		Объект.NumberColumnsInKey = 1;
		Объект.PeriodType = 0;
		Объект.AbsolutePeriodValue.ДатаНачала = НачалоМесяца(ТекущаяДата());
		Объект.AbsolutePeriodValue.ДатаОкончания = КонецДня(ТекущаяДата());
		Объект.RelativePeriodValue = 0;
		Объект.ValueOfSlaveRelativePeriod = 0;
		Объект.DiscretenessOfRelativePeriod = "month";
		Объект.DiscretenessOfSlaveRelativePeriod = "month";
		Объект.ConnectingToExternalBaseADriverSQL = "SQL Server";
		Объект.ConnectingToExternalBaseBDriverSQL = "SQL Server";
		Объект.NumberFirstRowFileA = 1;
		Объект.NumberFirstRowFileB = 1;
		Объект.NumberOfRowsWithEmptyKeysToBreakReading = 2;
		Объект.ConnectingToExternalBaseADeviceStorageFile = 0;
		Объект.ConnectingToExternalBaseBDeviceStorageFile = 0;
		Объект.ConnectionToExternalDatabaseANumberTableInFile = 1;
		Объект.ConnectionToExternalDatabaseBNumberTableInFile = 1;
		
		Для Счетчик = 1 По 5 Цикл
			Объект["VisibilityAttributeA" + Счетчик] = Истина;
			Объект["VisibilityAttributeB" + Счетчик] = Истина;
		КонецЦикла;
		
		Для Счетчик = 1 По 20 Цикл 
			
			Объект.TableA.Область(1,Счетчик,1,Счетчик).Текст = Счетчик;
			Объект.TableB.Область(1,Счетчик,1,Счетчик).Текст = Счетчик;
			
		КонецЦикла;
		
	КонецЕсли;
		
	Пример1 = "КлючТек = Лев(КлючТек,10);";
	Пример2 = "КлючТек = Число(КлючТек) + 1;";
	Пример3 = "Если Лев(КлючТек,1) = ""#"" Тогда КлючТек = Сред(КлючТек, 2); КонецЕсли;";
	Пример4 = "КлючТек = Прав(""0000000000"" + КлючТек, 10);";
	Пример5 = "КлючТек = СтрЗаменить(КлючТек, ""_"", """");";
	Пример6 = "КлючТек = ?(ЗначениеЗаполнено(КлючТек), КлючТек, ""<>"");";
	
	Если Объект.UserMode Тогда
		
		Элементы.ГруппаШапкаСкрываемыеРеквизиты.Видимость = Ложь;
		Элементы.ГруппаБазаАСтраница.Видимость = Ложь;
		Элементы.ГруппаБазаБСтраница.Видимость = Ложь;
		Элементы.ГруппаНастройкиВывода.Видимость = Ложь;
		Элементы.ГруппаОсновная.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		Элементы.РезультатКомандаВыгрузитьРезультатВФайлНаСервере.Видимость = Ложь;
		Элементы.РезультатГруппаВидимостьСтолбцовКлюча.Видимость = Ложь;
				
	Иначе		
	
		МакетКартинкаАктивнаяОперация1 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация1");
		МакетКартинкаНеактивнаяОперация1 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация1");
		МакетКартинкаАктивнаяОперация2 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация2");
		МакетКартинкаНеактивнаяОперация2 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация2");
		МакетКартинкаАктивнаяОперация3 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация3");
		МакетКартинкаНеактивнаяОперация3 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация3");
		МакетКартинкаАктивнаяОперация4 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация4");
		МакетКартинкаНеактивнаяОперация4 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация4");
		МакетКартинкаАктивнаяОперация5 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация5");
		МакетКартинкаНеактивнаяОперация5 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация5");
		МакетКартинкаАктивнаяОперация6 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация6");
		МакетКартинкаНеактивнаяОперация6 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация6");
		МакетКартинкаАктивнаяОперация7 		= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаАктивнаяОперация7");
		МакетКартинкаНеактивнаяОперация7 	= РеквизитФормыВЗначение("Объект").ПолучитьМакет("КартинкаНеактивнаяОперация7");
			
		АктивнаяОперация1 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация1, УникальныйИдентификатор);
		НеактивнаяОперация1 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация1, УникальныйИдентификатор);
		АктивнаяОперация2 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация2, УникальныйИдентификатор);
		НеактивнаяОперация2 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация2, УникальныйИдентификатор);
		АктивнаяОперация3 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация3, УникальныйИдентификатор);
		НеактивнаяОперация3 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация3, УникальныйИдентификатор);
		АктивнаяОперация4 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация4, УникальныйИдентификатор);
		НеактивнаяОперация4 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация4, УникальныйИдентификатор);
		АктивнаяОперация5 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация5, УникальныйИдентификатор);
		НеактивнаяОперация5 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация5, УникальныйИдентификатор);
		АктивнаяОперация6 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация6, УникальныйИдентификатор);
		НеактивнаяОперация6 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация6, УникальныйИдентификатор);
		АктивнаяОперация7 	= ПоместитьВоВременноеХранилище(МакетКартинкаАктивнаяОперация7, УникальныйИдентификатор);
		НеактивнаяОперация7 = ПоместитьВоВременноеХранилище(МакетКартинкаНеактивнаяОперация7, УникальныйИдентификатор); 
		
	КонецЕсли;

	UT_Common.ToolFormOnCreateAtServer(ЭтотОбъект, Отказ, СтандартнаяОбработка,
		Элементы.ГруппаПанель2);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимостьДоступностьЭлементовФормы();
	ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация();
	ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();
	
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
		
КонецПроцедуры

&НаКлиенте
Процедура ОперацияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.RelationalOperation = Число(Прав(Элемент.Имя,1));
	ОбновитьВидимостьДоступностьЭлементовРеляционнаяОперация();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если Не ЗакрытиеФормыПодтверждено Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма),"Закрыть консоль сравнения данных?", РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗначениеЗаполнено(Объект.RelatedDataComparisonOperation) И Не Объект.UserMode  Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("СохранитьВСвязаннуюОперациюЗавершение", ЭтотОбъект, Новый Структура("ВыбратьЭлементСправочникаДляСохранения,СохранятьТабличныеДокументы,ПриЗакрытииФормы",Истина,Ложь,Истина)), "Обновить элемент справочника """ + Объект.RelatedDataComparisonOperation + """?", РежимДиалогаВопрос.ДаНет);
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПосетитьСтраницу(КодВозврата, ДополнительныеПараметры) Экспорт
	
	

КонецПроцедуры

&НаКлиенте
Процедура КодДляВыводаСтрокРедактируетсяВручнуюПриИзменении(Элемент)
	
	Если Не Объект.CodeForOutputRowsEditedManually Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("КодДляВыводаСтрокРедактируетсяВручнуюПриИзмененииЗавершение", ЭтаФорма), "Код, внесенный вручную будет утерян. Продолжить?", РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	КодДляВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура КодДляВыводаСтрокРедактируетсяВручнуюПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Объект.CodeForOutputRowsEditedManually = Истина;
		Возврат;
	КонецЕсли;
	
	
	КодДляВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура КодДляВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент()
	
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
	ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();

КонецПроцедуры

&НаКлиенте
Процедура CodeForProhibitingOutputRowsEditedManuallyOnChange(Элемент)
	
	Если Не Объект.CodeForProhibitingOutputRowsEditedManually Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("КодДляЗапретаВыводаСтрокРедактируетсяВручнуюПриИзмененииЗавершение", ЭтаФорма), "Код, внесенный вручную будет утерян. Продолжить?", РежимДиалогаВопрос.ДаНет);
        Возврат;
		
	КонецЕсли;
	
	КодДляЗапретаВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент();
КонецПроцедуры

&НаКлиенте
Процедура КодДляЗапретаВыводаСтрокРедактируетсяВручнуюПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Объект.CodeForProhibitingOutputRowsEditedManually = Истина;
		Возврат;
	КонецЕсли;
	
	
	КодДляЗапретаВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура КодДляЗапретаВыводаСтрокРедактируетсяВручнуюПриИзмененииФрагмент()
	
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
	ОбновитьВидимостьДоступностьЭлементовВыводИЗапретаВыводаСтрок();

КонецПроцедуры

&НаКлиенте
Процедура ConditionsOutputRowsOnChange(Элемент)
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
КонецПроцедуры

&НаКлиенте
Процедура ConditionsProhibitOutputRowsOnChange(Элемент)
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
КонецПроцедуры

&НаКлиенте
Процедура BooleanOperatorForProhibitingConditionsOutputRowsOnChange(Элемент)
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
КонецПроцедуры

&НаКлиенте
Процедура ЛогическийОператорДляУсловийВыводаСтрокПриИзменении(Элемент)
	ОбновитьКодДляВыводаИЗапретаВыводаСтрок();
КонецПроцедуры

&НаКлиенте
Процедура КомандаВидимостьКолонкиТЧ(Команда)
	
	AttributeName = СтрЗаменить(Команда.Имя, "КомандаВидимость", "");
	
	Объект["Видимость" + AttributeName] = Не Объект["Видимость" + AttributeName];
	
	ОбновитьВидимостьРеквизитаТЧ(AttributeName);
		
КонецПроцедуры

&НаКлиенте
Процедура ТипПараметраПериодПриИзменении(Элемент)
	
	RefreshDataPeriod();
	ОбновитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура RelativePeriodValueOnChange(Элемент)
	
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура DiscretenessOfRelativePeriodOnChange(Элемент)
	
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеКВнешнейБазеАПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Если ПустаяСтрока(Объект.ConnectionToExternalBaseAFileFormat) Тогда
		ДиалогВыбораФайла.Фильтр = 
			"*.*" + 
			"|*.*";
	ИначеЕсли Объект.ConnectionToExternalBaseAFileFormat = "XLS" Или Объект.ConnectionToExternalBaseAFileFormat = "DOC" Тогда
		ДиалогВыбораФайла.Фильтр = 
			"*." + Объект.ConnectionToExternalBaseAFileFormat
			+ ";*." + Объект.ConnectionToExternalBaseAFileFormat + "X"
			+ "|*." + Объект.ConnectionToExternalBaseAFileFormat
			+ ";*." + Объект.ConnectionToExternalBaseAFileFormat + "X";
	Иначе
		ДиалогВыбораФайла.Фильтр = 
			"*." + Объект.ConnectionToExternalBaseAFileFormat + 
			"|*." + Объект.ConnectionToExternalBaseAFileFormat;
	КонецЕсли;
		
	ДиалогВыбораФайла.Title = "Выберите файл";
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ПодключениеКВнешнейБазеАПутьКФайлуНачалоВыбораЗавершение", ЭтаФорма, Новый Структура("ДиалогВыбораФайла", ДиалогВыбораФайла)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключениеКВнешнейБазеАПутьКФайлуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.ConnectionToExternalBaseAPathToFile = ДиалогВыбораФайла.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ConnectionToExternalBaseBPathToFileStartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	Если ПустаяСтрока(Объект.ConnectionToExternalBaseBFileFormat) Тогда
		ДиалогВыбораФайла.Фильтр = 
			"*.*" + 
			"|*.*";
	ИначеЕсли Объект.ConnectionToExternalBaseBFileFormat = "XLS" Или Объект.ConnectionToExternalBaseBFileFormat = "DOC" Тогда
		ДиалогВыбораФайла.Фильтр = 
			"*." + Объект.ConnectionToExternalBaseBFileFormat
			+ ";*." + Объект.ConnectionToExternalBaseBFileFormat + "X"
			+ "|*." + Объект.ConnectionToExternalBaseBFileFormat
			+ ";*." + Объект.ConnectionToExternalBaseBFileFormat + "X";
	Иначе
		ДиалогВыбораФайла.Фильтр = 
			"*." + Объект.ConnectionToExternalBaseBFileFormat + 
			"|*." + Объект.ConnectionToExternalBaseBFileFormat;
	КонецЕсли;
		
	ДиалогВыбораФайла.Title = "Выберите файл";
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ConnectionToExternalBaseBPathToFileStartChoiceEnd", ЭтаФорма, Новый Структура("ДиалогВыбораФайла", ДиалогВыбораФайла)));
	
КонецПроцедуры

&НаКлиенте
Процедура ConnectionToExternalBaseBPathToFileStartChoiceEnd(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.ConnectionToExternalBaseBPathToFile = ДиалогВыбораФайла.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура SettingsFileABeforeAddRow(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Объект.SettingsFileA.Count() = 5 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура SettingsFileBBeforeAddRow(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Объект.SettingsFileB.Count() = 5 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКлючевогоРеквизита(Элемент)
	
	ОбновитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ParameterListAParameterValueStartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Элементы.ParameterListAParameterValue.ВыбиратьТип = ТипЗнч(Элементы.ParameterListA.ТекущиеДанные.ParameterValue) = Тип("Неопределено");	
	ПриНачалеВыбораЗначенияПараметра("А", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ParameterListAParameterValueOnChange(Элемент)
	
	ТекущийПараметр = Объект.ParameterListA.НайтиПоИдентификатору(Элементы.ParameterListA.ТекущиеДанные.ПолучитьИдентификатор());
	ТекущийПараметр.ParameterType = ТипЗнч(ТекущийПараметр.ParameterValue);

КонецПроцедуры

&НаКлиенте
Процедура ParameterListBParameterValueStartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Элементы.ParameterListBParameterValue.ВыбиратьТип = ТипЗнч(Элементы.ParameterListB.ТекущиеДанные.Значениепараметра) = Тип("Неопределено");	
	ПриНачалеВыбораЗначенияПараметра("Б", СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ParameterListBParameterValueOnChange(Элемент)
	
	ТекущийПараметр = Объект.ParameterListB.НайтиПоИдентификатору(Элементы.ParameterListB.ТекущиеДанные.ПолучитьИдентификатор());
	ТекущийПараметр.ParameterType = ТипЗнч(ТекущийПараметр.ParameterValue);
	
КонецПроцедуры

&НаКлиенте
Процедура NumberColumnsInKeyOnChange(Элемент)
	
	ОбновитьВидимостьДоступностьЭлементовФормы();
	ОбновитьВидимостьДоступностьЭлементовФормыПоИдентификаторуБазы("А");
	ОбновитьВидимостьДоступностьЭлементовФормыПоИдентификаторуБазы("Б");
	
КонецПроцедуры

&НаКлиенте
Процедура ResultKeyStartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ResultKey2StartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ResultKey3StartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ResultKeyClearing(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ResultKey2Clearing(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ResultKey3Clearing(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииФлагаВыполнятьПроизвольныйКодКлюча(Элемент)
	
	ОбновитьРеквизитыПроизвольныйКод();
	
КонецПроцедуры

&НаКлиенте
Процедура DisplayKeyColumnTypesOnChange(Элемент)
	
	ОбновитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВидимостьТиповСтолбцовКлюча(Команда)
	
	Элементы.РезультатКомандаВидимостьТиповСтолбцовКлюча.Пометка = Не Элементы.РезультатКомандаВидимостьТиповСтолбцовКлюча.Пометка;
	Объект.DisplayKeyColumnTypes = Элементы.РезультатКомандаВидимостьТиповСтолбцовКлюча.Пометка;
	Если Объект.DisplayKeyColumnTypes Тогда
		ЗаполнитьТипыСтолбцовКлючаВоВсехСтроках();
	КонецЕсли;
	ОбновитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьРезультатВФайлНаСервере(Команда)
	
	Если ПустаяСтрока(Объект.UploadFileFormat) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Поле = "Объект.UploadFileFormat";
		СообщениеПользователю.Текст = "Не задан формат файла выгрузки";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.PathToDownloadFile) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Поле = "Объект.PathToDownloadFile";
		СообщениеПользователю.Текст = "Не задан путь к файлу выгрузки";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
			
	Ответ = Неопределено; 	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаВыгрузитьРезультатВФайлНаСервереЗавершение", ЭтаФорма), "Выгрузить таблицу в файл на сервере?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, "Выгрузка");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьРезультатВФайлНаСервереЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса; 
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьРезультатВФайлНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура PathToDownloadFileStartChoice(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПустаяСтрока(Объект.UploadFileFormat) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Поле = "Объект.UploadFileFormat";
		СообщениеПользователю.Текст = "Не задан формат файла выгрузки";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.Сохранение;
	ДиалогВыбора = Новый ДиалогВыбораФайла(Режим);
	ДиалогВыбора.ПолноеИмяФайла = Объект.Title;
	Фильтр = "Файл " + Объект.UploadFileFormat + " (*." + Объект.UploadFileFormat + ")|*." + Объект.UploadFileFormat + "";
	ДиалогВыбора.Фильтр = Фильтр;
	ДиалогВыбора.Title = "Укажите файл для сохранения результата сравнения";   

	ДиалогВыбора.Показать(Новый ОписаниеОповещения("ПутьКФайлуВыгрузкиНачалоВыбораЗавершение", ЭтаФорма, Новый Структура("ДиалогВыбора", ДиалогВыбора)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуВыгрузкиНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбора = ДополнительныеПараметры.ДиалогВыбора;	
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		
		Объект.PathToDownloadFile =  ДиалогВыбора.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьРезультатВФайлНаКлиенте(Команда)
	
	Если ПустаяСтрока(Объект.UploadFileFormat) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Поле = "Объект.UploadFileFormat";
		СообщениеПользователю.Текст = "Не задан формат файла выгрузки";
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаВыгрузитьРезультатВФайлНаКлиентеЗавершениеВопрос", ЭтаФорма), "Выгрузить таблицу в файл на клиенте?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет, "Выгрузка");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПериодаПриИзменении(Элемент)
	
	RefreshDataPeriod();
	ОбновитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ValueOfSlaveRelativePeriodOnChange(Элемент)
	
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура DiscretenessOfSlaveRelativePeriodOnChange(Элемент)
	
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура DiscretenessOfRelativePeriodClearing(Элемент, СтандартнаяОбработка)
	
	Объект.DiscretenessOfRelativePeriod = "day";
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура DiscretenessOfSlaveRelativePeriodClearing(Элемент, СтандартнаяОбработка)
	
	Объект.DiscretenessOfSlaveRelativePeriod = "day";
	RefreshDataPeriod();
	
КонецПроцедуры

&НаКлиенте
Процедура ConditionsOutputRowsDisabledOnChange(Элемент)
	
	ОбновитьВидимостьДоступностьВкладкиУсловияВыводаСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура ConditionsProhibitOutputRowsDisabledOnChange(Элемент)
	
	ОбновитьВидимостьДоступностьВкладкиУсловияЗапретаВыводаСтрок();
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Attachable_ExecuteToolsCommonCommand(Команда) 
	UT_CommonClient.Attachable_ExecuteToolsCommonCommand(ЭтотОбъект, Команда);
КонецПроцедуры



#КонецОбласти

ЗакрытиеФормыПодтверждено = Ложь;